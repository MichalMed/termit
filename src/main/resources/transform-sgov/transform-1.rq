PREFIX g: <__VOC_IRI__>

PREFIX v-sgov-pojem: <https://slovník.gov.cz/veřejný-sektor/pojem/>
PREFIX popis-dat-pojem: <http://onto.fel.cvut.cz/ontologies/slovnik/agendovy/popis-dat/pojem/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX dce: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
DELETE {
    ?x dc:created ?y .
	?c dc:created ?y2 .
	?d dc:created ?y3 .
}
INSERT {
    GRAPH g: {
    ?x a popis-dat-pojem:dokumentovy-slovnik ;
       popis-dat-pojem:ma-glosar ?g ;
       popis-dat-pojem:ma-model ?m ;
       popis-dat-pojem:popisuje-dokument ?d ;
       popis-dat-pojem:ma-autora ?a ;
       dc:created ?t ;
       rdfs:label ?vTitle .
        
    ?d a popis-dat-pojem:dokument ;
       rdfs:label ?dTitle ;
       dce:description ""@cs ;
       popis-dat-pojem:ma-autora ?a ;
       dc:created ?t ;
       popis-dat-pojem:ma-soubor ?s .

    ?s a popis-dat-pojem:soubor ;
       rdfs:label ?sTitle ;
       dce:description ""@cs ;
       popis-dat-pojem:ma-autora ?a ;
       dc:created ?t .
        
    ?m a popis-dat-pojem:model .
    ?g a popis-dat-pojem:glosar .
    ?c a popis-dat-pojem:term ;
       rdfs:label ?l ;
       popis-dat-pojem:ma-autora ?a ;
       dc:created ?t ;
       popis-dat-pojem:je-pojmem-ze-slovniku ?x .
    ?g popis-dat-pojem:obsahuje-korenovy-pojem ?cTop .
    }
} WHERE { 
    GRAPH g: {
    ?x a v-sgov-pojem:slovník ;
       v-sgov-pojem:má-glosář ?g ;
       v-sgov-pojem:má-model ?m ;
       dce:title ?vTitle .
    ?c skos:inScheme ?g ;
	   skos:prefLabel ?l .
	FILTER(lang(?vTitle)="cs")
    OPTIONAL {
        ?x dc:created ?y .
	}
    OPTIONAL {
        ?c dc:created ?y2 .
	}
    OPTIONAL {
        FILTER NOT EXISTS {
            ?c skos:broader|^skos:narrower ?cSuper.
            FILTER (strstarts(str(?cSuper),g:))
        }
        BIND( ?c AS ?cTop )
	}
    BIND (<http://onto.fel.cvut.cz/ontologies/uzivatel/petr-kremen> as ?a)
    BIND (iri(concat(str(?x),"/document")) as ?d)
    BIND (strlang(concat("Dokument pro ",str(?vTitle)),"cs") as ?dTitle)
    BIND (iri(concat(str(?d),"/file")) as ?s)
    BIND (strlang(concat("Soubor pro ",str(?vTitle)),"cs") as ?sTitle)
    BIND (NOW() as ?t)
    OPTIONAL {
        ?d dc:created ?y3 .
	}
}
}